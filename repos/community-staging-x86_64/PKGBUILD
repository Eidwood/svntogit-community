# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: m0rph <m0rph.mailbox@gmail.com>

pkgname=goldendict
pkgver=1.5.0RC2
pkgrel=10
pkgdesc="Feature-rich dictionary lookup program"
arch=('x86_64')
url="http://goldendict.org"
license=('GPL3')
depends=('hunspell' 'libxtst' 'libzip' 'libao' 'qt5-webkit' 'qt5-svg'
         'qt5-x11extras' 'qt5-tools' 'phonon-qt5' 'ffmpeg')
provides=('stardict')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/1.5.0-RC2.tar.gz
        goldendict-qt-5.11-1.patch::"https://github.com/goldendict/goldendict/commit/3d4a468b.patch"
        goldendict-qt-5.11-2.patch::"https://github.com/goldendict/goldendict/commit/a6596780.patch"
        goldendict-ffmpeg4.patch::"https://github.com/goldendict/goldendict/commit/03bbe01b.patch"
        ffmpeg5.patch)
sha256sums=('bccee0a3d3902f8fa31e439e220a405065fff774e5f8c581be2b0743d3f83fde'
            '4606d4ce6022fc0e7b6d3dec823e1eefcd30f0760de8241a89e36dd65d47d73d'
            '1ef4bf9a92ce407d50476260988253e98f0e17fa2b9acc863f8a5b536924deb2'
            'fa4d004c021c1987afb107ce3480c518b8e350630b278343a1e32dc50564b633'
            '0d670656872a1558530e013f30cf88c63eb879819f5ed3b09d48e8d20b4095a7')

prepare() {
  cd $pkgname-1.5.0-RC2
  patch -p1 -i ../goldendict-qt-5.11-1.patch # Fix build with Qt 5.11
  patch -p1 -i ../goldendict-qt-5.11-2.patch # Follow-up to the above
  patch -p1 -i ../goldendict-ffmpeg4.patch # Fix build with ffmpeg 4
  patch -p1 -i ../ffmpeg5.patch # Fix build with ffmpeg 5
}

build(){
  cd "${srcdir}"/$pkgname-1.5.0-RC2

  export CXXFLAGS+=" -std=c++14" # Fix build with GCC 11, fixed in master
  qmake-qt5 "CONFIG+=no_epwing_support" PREFIX="/usr" 
  make
}

package() {
  cd "${srcdir}"/$pkgname-1.5.0-RC2

  make INSTALL_ROOT="${pkgdir}" install

# languages
  install -d "${pkgdir}"/usr/share/$pkgname/locale
  install -m644 locale/*.qm -t "${pkgdir}"/usr/share/$pkgname/locale

# removing unneeded
  rm -rf "${pkgdir}"/usr/share/app-install
}
